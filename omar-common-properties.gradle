import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage

buildscript {
    ext {
        grailsVersion = project.grailsVersion
        artifactoryContextUrl="https://artifacts.radiantbluecloud.com/artifactory"
        ossimMavenProxy="${artifactoryContextUrl}/ossim-deps"
    }
    repositories {

     mavenLocal()
     maven { url "${ossimMavenProxy}" }
      jcenter()
      maven {
        url "https://plugins.gradle.org/m2/"
      }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
        classpath 'com.github.skhatri:gradle-s3-plugin:1.0.4'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
   }
}

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin
apply plugin: com.github.skhatri.s3aws.plugin.S3Plugin
apply plugin: org.jfrog.gradle.plugin.artifactory.ArtifactoryPlugin

ext {
  buildVersionTag="SNAPSHOT" // SNAPSHOT or RELEASE
  jarArtifactsChanging="${buildVersionTag}" == "SNAPSHOT" ? true:false
}

def artifactoryUser="${System.env.ARTIFACTORY_USER}"
def artifactoryPassword="${System.env.ARTIFACTORY_PASSWORD}"

def openShiftUrl="https://openshift-master.ossim.io:8443"
def dockerAppTag=(buildVersionTag=="SNAPSHOT"?"latest":"release")

def dockerRegistryUrl="docker-registry-default.ossim.io"
def registryProjectName="o2"

def openShiftUserName="${System.env.OPENSHIFT_USERNAME}"
def openShiftPassword="${System.env.OPENSHIFT_PASSWORD}"

def dockerNamespaceUrl = "${dockerRegistryUrl}/${registryProjectName}/"

def image = "${project.name}:${dockerAppTag}"
def imageTgz = "${project.name}.tgz"

def installOssim = true

version "${buildVersion}-${buildVersionTag}"

task downloadBaseImage(type:com.github.skhatri.s3aws.plugin.S3DownloadTask, dependsOn: 'assemble') {
  bucket = 'o2-delivery/dev/docker'
  key = "${project.ext.baseImage}.tgz"
  saveTo = file("build/libs/${project.ext.baseImage}.tgz")
}

task loadBaseDockerImage(dependsOn: downloadBaseImage) {

  doLast {
    exec {
      commandLine 'docker', 'load', '-i', "build/libs/${project.ext.baseImage}.tgz"
    }
  }
}

task createDockerfile(type: Dockerfile, dependsOn: loadBaseDockerImage) {
  def jarName = "${project.name}-${buildVersion}-${buildVersionTag}.jar"

  destFile = project.file('build/libs/Dockerfile')
  from project.ext.baseImage:${dockerAppTag}
  maintainer "DigitalGlobe-RadiantBlue"
  runCommand 'useradd omar'
  copyFile jarName, "/home/omar"
  user 'omar'
  workingDir '/home/omar'
  environmentVariable "SPRING_CLOUD_CONFIG_ENABLED","true"
  environmentVariable "SPRING_CLOUD_DISCOVERY_ENABLED","true"
  exposePort 8080
  defaultCommand 'java', '-server', '-Xms256m', '-Xmx1024m', '-Djava.awt.headless=true', '-XX:+CMSClassUnloadingEnabled', '-XX:+UseGCOverheadLimit', '-Djava.security.egd=file:/dev/./urandom', '-jar', jarName
}

task buildDockerImage(type: DockerBuildImage, dependsOn: createDockerfile) {
    inputDir = createDockerfile.destFile.parentFile
    tag = "${image}"
}

task tagDockerImage(type: DockerTagImage, dependsOn: buildDockerImage) {
  imageId "${image}"
  tag "${dockerAppTag}"
  repository  "${dockerNamespaceUrl}${project.name}"
}

task logIn (dependsOn: tagDockerImage) {
  doLast {
    def login = false
    def count = 0
    def numRetries = 3
    def sleepMillis = 1000
    while(!login && count < numRetries){
      try{
        exec {
          commandLine 'oc', 'login', '--insecure-skip-tls-verify', openShiftUrl,
           '-u', openShiftUserName,
           '-p', openShiftPassword
        }
        login = true
      }
      catch(e){

        println 'Openshift login failed!'
        count++
        if(count < numRetries){
          Thread.sleep(sleepMillis)
        }
        else {
          throw(e)
        }
      }
    }

    def loginTokenStream = new ByteArrayOutputStream()
    exec {
      commandLine 'oc', 'whoami', '-t'
      standardOutput loginTokenStream
    }

    def loginToken = new String(loginTokenStream.toByteArray(), 'UTF-8')?.trim()
    println loginToken
    exec {
     commandLine 'docker', 'login', '-p', loginToken, '-u', 'unused', dockerRegistryUrl
    }
  }
}

task pushDockerImage(type: DockerPushImage, dependsOn: logIn) {
  imageName "${dockerNamespaceUrl}${image}"
}

task saveDockerImage(dependsOn: pushDockerImage) {
  doLast {
    exec {
      commandLine 'docker', 'save', image, '-o', "build/libs/${project.name}.tgz"
    }
  }
}

// Uses the S3 plugin to push the .tgz file to the ossimlabs
task dockerImageToS3(type: com.github.skhatri.s3aws.plugin.S3UploadTask, dependsOn: saveDockerImage) {
  bucket = 'o2-delivery/dev/docker'
  key = "${imageTgz}"
  file = project.file("build/libs/${imageTgz}").toString()
}

task doAll(dependsOn: dockerImageToS3) {
  doLast{
    exec {
      // Removes the image that is pushed to the remote repository
      // from the local Docker registry
      commandLine 'docker', 'rmi', "-f", "${dockerNamespaceUrl}${image}"
    }
    exec {
      // Removes the default image created during the build process from
      // from the local Docker registry
      commandLine 'docker', 'rmi', "-f", image
    }
    exec {
      // Removes the 'o2-base' image used during the build process
      commandLine 'docker', 'rmi', "-f", project.ext.baseImage
    }
    exec {
      // Removes the zipped image that was pushed to S3
      commandLine 'rm', "build/libs/${imageTgz}"
    }
    exec {
      // Removes the base image zip file
      commandLine 'rm', "build/libs/${project.ext.baseImage}.tgz"
    }
  }
}

artifactory {
  contextUrl = "${artifactoryContextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
  publish {
    repository {
      repoKey = "omar-local"
      username = "${artifactoryUser}"
      password = "${artifactoryPassword}"
      maven = true
    }
    defaults{
      publications("mavenJava")
    }
  }
  resolve {
    repository {
      repoKey = "omar-local"
      username = "${artifactoryUser}"
      password = "${artifactoryPassword}"
      maven = true
    }
  }
}
